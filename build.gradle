plugins {
	id 'java'
	id 'groovy'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.0'
}

group = 'com.albertolopez'
version = '0.0.1-SNAPSHOT'
description = 'Jackpot bets project'

java {
	sourceCompatibility = '21'
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
		}
		groovy {
			srcDirs = ['src/integrationTest/groovy']
		}
		resources {
			srcDirs = ['src/integrationTest/resources']
		}
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
	mavenCentral()
	jcenter()
	maven {
		url "https://packages.confluent.io/maven/"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.flywaydb:flyway-core'
	implementation 'com.h2database:h2'
	implementation 'org.projectlombok:lombok'
	implementation 'org.apache.avro:avro:1.11.1'
	implementation 'io.confluent:kafka-avro-serializer:7.5.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation("org.spockframework:spock-spring:2.4-M6-groovy-4.0")

	integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
	integrationTestImplementation("org.spockframework:spock-spring:2.4-M6-groovy-4.0")
	integrationTestImplementation 'org.testcontainers:testcontainers:1.19.3'
	integrationTestImplementation 'org.testcontainers:kafka:1.19.3'
	integrationTestImplementation 'org.springframework.kafka:spring-kafka-test'
	integrationTestImplementation 'org.testcontainers:spock:1.19.3'
	integrationTestImplementation 'io.rest-assured:rest-assured:5.4.0'
	integrationTestImplementation 'io.rest-assured:json-path:5.4.0'
	integrationTestImplementation 'org.awaitility:awaitility:4.2.0'
	integrationTestImplementation 'org.testcontainers:junit-jupiter:1.19.3'
}

task integrationTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
	shouldRunAfter test
}

check {
	dependsOn integrationTest
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('integrationTest') {
	useJUnitPlatform()
}